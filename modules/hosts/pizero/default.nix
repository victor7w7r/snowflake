{
  den,
  inputs,
  hosts,
  kernel,
  tarball,
  sdcard,
  ...
}:
{
  #mount /dev/sde1 /mnt && rm -rf /mnt/* && tar --zstd -xvf boot.tar.zst -C /mnt/ --no-same-owner && sync && umount /dev/sde1 && udisksctl power-off -b /dev/sde
  #mount -o noatime,nodiratime,lazytime,discard=async,compress-force=zstd:3,subvol=@nix /dev/sde2 /mnt && rm -rf /mnt/store/* && tar --zstd -xvf store.tar.zst -C /mnt/store/ && sync && umount /dev/sde2 && udisksctl power-off -b /dev/sde

  perSystem.packages = {
    pizero-toplevel = inputs.self.nixosConfigurations.pizero.config.system.build.toplevel;
    pizero-image = inputs.self.nixosConfigurations.pizero-sdimage.config.system.build.sdImage;
    pizero-mktarball = inputs.self.nixosConfigurations.pizero-tarball.config.system.build.tarball;
    pizero-boot = inputs.self.nixosConfigurations.pizero-tarball.config.system.build.bootFiles;
  };

  den = {
    hosts.aarch64-linux = {
      pizero.users = {
        #root = { };
        victor7w7r = { };
      };
      pizero-sdimage.users = {
        #root = { };
        victor7w7r = { };
      };
      pizero-tarball.users = {
        #root = { };
        victor7w7r = { };
      };
    };

    aspects = {
      pizero-sdimage.includes = with den.aspects; [
        pizero.common
        (sdcard.lib.call { })
      ];
      pizero-tarball.includes = with den.aspects; [
        pizero.common
        (tarball.lib.call { })
      ];
      pizero = {
        includes = with den.aspects; [ pizero.common ];
        common = {
          includes = with den.aspects; [
            (hosts.lib.zram {
              value = "256M";
              memoryPercent = 100;
            })
            (hosts.lib.static-network "eth0" "11")
            pizero._

            cli._
            containers
            dev.mise
            dev.tools
            dev.ccache
            disks
            misc.comm
            misc.fetch
            pentest._

            cockpit
            emulation
            firewall
            games
            remote
            root
            tools
            victor7w7r
          ];

          nixos =
            {
              lib,
              pkgs,
              self',
              ...
            }:
            {
              networking.hostName = "v7w7r-opizero2w";
              environment.persistence."/nix/persist" = {
                users = {
                  "victor7w7r".directories = lib.mkForce [
                    "repositories"
                    "remote"
                    ".config/nix"
                    ".local/share/cod"
                    ".local/share/zoxide"
                    ".ssh"
                    ".gnupg"
                  ];
                  root.directories = lib.mkForce [ ".zsh" ];
                };
              };

              nix.settings.max-jobs = lib.mkDefault 2;

              hardware = {
                firmware = with self'.packages; lib.singleton uwe5622-firmware;
                deviceTree = {
                  name = "allwinner/sun50i-h618-orangepi-zero2w.dtb";
                  overlays =
                    map
                      (dtso: {
                        name = "${inputs.armbian}/patch/kernel/archive/sunxi-6.18/overlay_64/sun50i-h616-${dtso}";
                        dtsFile = "${inputs.armbian}/patch/kernel/archive/sunxi-6.18/overlay_64/sun50i-h616-${dtso}.dtso";
                      })
                      [
                        "i2c2-ph"
                        "i2c3-pg"
                        "i2c3-ph"
                        "i2c4-pg"
                        "i2c4-ph"
                        "keys"
                        "pwm1-ph3"
                        "pwm1-pi11"
                        "pwm2-ph2"
                        "pwm2-pi12"
                        "pwm3-ph0"
                        "pwm3-pi13"
                        "uart2-pg"
                        "uart2-pg-rts-cts"
                        "uart2-ph"
                        "uart2-ph-rts-cts"
                        "uart2-pi"
                        "uart2-pi-rts-cts"
                        "uart3-pi"
                        "uart3-pi-rts-cts"
                        "uart4-pi"
                        "uart4-pi-rts-cts"
                        "uart5"
                      ];
                };
              };

              systemd.tmpfiles.rules = [
                "L+ /lib/firmware/uwe5622 - - - - /run/current-system/firmware"
                "L+ /lib/firmware/wcnmodem.bin - - - - /run/current-system/firmware/wcnmodem.bin"
                "L+ /lib/firmware/wifi_2355b001_1ant.ini - - - - /run/current-system/firmware/wifi_2355b001_1ant.ini"
              ];

              powerManagement.cpuFreqGovernor = "schedutil";

              boot = {
                blacklistedKernelModules = [ "sun8i_ce" ];
                kernelModules = [
                  "sprdbt_tty"
                  "sprdwl_ng"
                ];
                kernelParams = [
                  "console=ttyS0,115200n8"
                  "loglevel=3"
                  #"resume=${config.boot.resumeDevice}"
                ];
                loader = {
                  grub.enable = false;
                  generic-extlinux-compatible.enable = true;
                };
                kernelPackages =
                  (kernel.hosts.pizero pkgs "pizero" "aarch64-linux" pkgs.stdenv.hostPlatform.system)
                  .pizero-kernelPackages;
              };

              services = {
                fail2ban.enable = lib.mkForce false;
                cockpit.allowed-origins = [
                  "https://192.168.100.11:9090"
                  "http://192.168.100.11:9090"
                  "https://100.64.0.4:9090"
                  "http://100.64.0.4:9090"
                ];
              };
              system.autoUpgrade.enable = lib.mkForce false;
            };
        };
      };
    };
  };
}
