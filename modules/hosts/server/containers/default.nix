{
  stateVersion,
  inputs,
  lib,
  ...
}:
{
  imports = [ (inputs.den.namespace "containers" false) ];

  containers.lib = {
    funnel =
      {
        pkgs,
        incoming ? null,
        incomingTcp ? null,
        outgoingTcp ? null,
      }:
      {
        wantedBy = [ "multi-user.target" ];
        after = [ "tailscaled.service" ];
        wants = [ "tailscaled.service" ];
        serviceConfig = {
          RestartSec = "10s";
          Restart = "on-failure";
          User = "root";
          ExecStart = ''
            ${pkgs.tailscale}/bin/tailscale funnel \
              ${lib.optionalString (incoming != null) ''
                https+insecure://localhost:${incoming} \
              ''}
              ${lib.optionalString (incomingTcp != null && outgoingTcp != null) ''
                --tcp=${outgoingTcp} tcp://localhost:${incomingTcp}
              ''}
          '';
        };
      };

    call =
      {
        ip,
        bindMounts,
        name,
        extra ? pkgs: { },
        containers ? config: { },
        forwardPorts ? [ ],
        imports ? [ ],
        services ? config: pkgs: { },
        secrets ? { },
        systemd ? pkgs: { },
        rules ? [ ],
      }:
      {
        autoStart = true;
        privateNetwork = true;
        enableTun = true;
        ephemeral = false;
        hostAddress = "10.200.1.1";
        localAddress = "10.200.1.${ip}";
        extraFlags = [
          "--private-users-ownership=chown"
          "--system-call-filter=@keyring"
          "--system-call-filter=bpf"
          "--system-call-filter=@network-io"
          "--system-call-filter=@system-service"
          "--capability=all"
        ];
        inherit forwardPorts;
        bindMounts = {
          "/etc/ssh" = {
            hostPath = "/home/victor7w7r/.ssh";
            isReadOnly = true;
          };

          "/var/lib/tailscale" = {
            hostPath = "/nix/persist/containers/${name}/tailscale";
            isReadOnly = false;
          };
        }
        // (lib.optionalAttrs (containers != null) {
          "/var/lib/docker" = {
            hostPath = "/nix/persist/containers/${name}/docker";
            isReadOnly = false;
          };
        })
        // bindMounts;

        config =
          { config, pkgs, ... }:
          {
            system.stateVersion = stateVersion;
            imports = [ inputs.agenix.nixosModules.default ] ++ imports;
            boot.isContainer = true;
            age = {
              identityPaths = [ "/etc/ssh/id_ed25519" ];
              secrets = {
                tailnet.file = ../secrets/tailnet.age;
              }
              // secrets;
            };
            networking = {
              hostName = "v7w7r-${name}";
              firewall.enable = false;
              useHostResolvConf = lib.mkForce false;
              defaultGateway = "10.200.1.1";
              nameservers = [
                "1.1.1.1"
                "8.8.8.8"
              ];
            };
            services = {
              resolved.enable = false;
              journald.settings.Journal = {
                SystemMaxUse = "100M";
              };
              timesyncd.enable = false;
              tailscale = {
                enable = true;
                openFirewall = true;
                useRoutingFeatures = "client";
                authKeyFile = config.age.secrets.tailnet.path;
                extraUpFlags = [
                  "--accept-dns=true"
                  "--accept-routes"
                ];
              };
            }
            // (services config pkgs);

            systemd = {
              tmpfiles.rules = rules;
              services = {
                tailscaled-autoconnect.serviceConfig = {
                  Type = lib.mkForce "exec";
                };
              }
              // (systemd pkgs);
            };
          }
          // (lib.optionalAttrs (containers != null) {
            virtualisation = {
              docker = {
                enable = true;
                extraOptions = "--storage-driver=vfs";
                autoPrune = {
                  enable = true;
                  dates = "weekly";
                };
                rootless = {
                  enable = false;
                  setSocketVariable = true;
                };
              };
              oci-containers = {
                containers = (containers config);
                backend = "docker";
              };
            };
          })
          // (extra pkgs);
      };
  };
}
